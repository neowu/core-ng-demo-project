apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'core.framework.demo'
    version = '1.0.0'

    repositories {
        maven {
            url 'https://neowu.github.io/maven-repo/'
        }
    }
}

def coreNGVersion = '6.9.2'
def kafkaVersion = '2.0.0'
def hsqlVersion = '2.4.1'

configure(subprojects.findAll { it.name.endsWith('-db-migration') }) {
    apply from: file("${rootDir}/gradle/db-migration.gradle")

    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.12'
    }
}

configure(subprojects.findAll { it.name.matches('.*-service-interface') }) {
    dependencies {
        implementation "core.framework:core-ng-api:${coreNGVersion}"
    }
}

configure([project(':demo-service'),
           project(':demo-site')]) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        runtimeOnly "org.apache.kafka:kafka-clients:${kafkaVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

project(':demo-service') {
    dependencies {
        implementation project(':demo-service-interface')
        runtimeOnly 'mysql:mysql-connector-java:8.0.12'
        testRuntimeOnly "org.hsqldb:hsqldb:${hsqlVersion}"
    }
}

project(':demo-site') {
    apply from: file("${rootDir}/gradle/node.gradle")
}

project(':benchmark') {
    dependencies {
        implementation 'org.openjdk.jmh:jmh-generator-annprocess:1.21'
        implementation "core.framework:core-ng:${coreNGVersion}"
        implementation 'io.undertow:undertow-core:2.0.13.Final'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.7'
        implementation 'com.fasterxml.jackson.module:jackson-module-afterburner:2.9.7'
    }
}
